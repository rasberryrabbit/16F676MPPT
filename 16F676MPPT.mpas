program _16F676MPPT;

{ 16F688 MPPT controller

  Copyright (C) 2018 rasberryrabbit

  This source is free software; you can redistribute it and/or modify it under
  the terms of the GNU General Public License as published by the Free
  Software Foundation; either version 2 of the License, or MPL 1.1
  any later version.

  This code is distributed in the hope that it will be useful, but WITHOUT ANY
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
  FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
  details.

  A copy of the GNU General Public License is available on the World Wide Web
  at <http://www.gnu.org/copyleft/gpl.html>. You can also obtain it by writing
  to the Free Software Foundation, Inc., 59 Temple Place - Suite 330, Boston,
  MA 02111-1307, USA.
}


{$define USE_EEPROM_LM358DIFF}
{.$define USE_RESET_TIMER}
{$define Write_OPAMP_DIFF}
{$define USE_4MHZ}
{.$define USE_OLD_METHOD}

const
   PWM_MAX = 255;            // 75
   PWM_MIN = 2;              // 1 -> 2, for low load condition.
   PWMHI_DIV = 6;            // 8
   PWMLO_DIV = 15;           // 14

   TMR1L_LOAD=$17;
   TMR1H_LOAD=$FC;           // 2ms

   Reset_Tick_Start = 1500;  // 5 minutes
   VOLMUL = 25 div 6;        // 25V(1024) : 6A(1024), don't exceed 8

   { circuit modified, 9.6mV / 3600mV * 1024 + 1, eeprom offset 0 => 04
     circuit v2 has more sensivity on current adc reading( x4 ),
     it may value 6 but 4. measuring on PIC pin 5 voltage while no loading  }
   cLM358_diff = 4;
   {$ifdef USE_OLD_METHOD}
   Inc_Pwm_Max =  8;          { >=5 }
   {$else}
   Inc_Pwm_Max = 1;
   {$endif}
   adc_max_loop = 2;

var
   LED1 : sbit at RC4_bit;
   PWM_SIG : sbit at RC1_bit;
   Write_OPAMP : sbit at RC3_bit;
   PWM_FLAG : bit;
   PWM_TICK, ON_PWM, VOL_PWM : byte;
   TICK_1000 : Byte;
   LED1_tm : byte;
   adc_vol, adc_cur, vol_prev, cur_prev : word;
   power_prev, power_curr : dword;
   i, LM358_diff : byte;
   flag_inc, p_equal : boolean;
   Inc_pwm : byte;
   vol1, vol2, lo_PWM, hi_PWM : byte;
   wPWM, wTemp : word;
   Reset_Tick100 : Byte;
   Reset_Tick : word;
   
procedure Interrupt(); iv 0x0004; ics ICS_AUTO;
begin
     if T0IF_bit=1 then begin
       if PWM_FLAG=1 then begin
         ON_PWM:=VOL_PWM;
         // on
         TMR0:=255-ON_PWM;
         PWM_SIG:=0;
         PWM_FLAG:=0;
       end else begin
         // off
         TMR0:=255-PWM_MAX+ON_PWM;
         PWM_SIG:=1;
         PWM_FLAG:=1;
       end;
       T0IF_bit:=0;
     end;
end;

begin
     CMCON:=7;
     ANSEL:=%00000101;       // ADC conversion clock = fRC, RA0=Current, RA2=Voltage
     TRISA0_bit:=1;
     TRISA1_bit:=1;          // Vref
     TRISA2_bit:=1;
     VCFG_bit:=1;
     CHS1_bit:=1;
     ADFM_bit:=1;
     
     TRISC1_bit:=0;          // PWM
     TRISC4_bit:=0;          // LED
     TRISC3_bit:=1;          // OP-AMP Cal

     LED1:=0;
     PWM_SIG:=1;
     PWM_FLAG:=1;
     LED1_tm:=250;
     ON_PWM:=0;
     VOL_PWM:=0;
     TICK_1000:=0;

     OPTION_REG:=%11011111;        // ~4KHz @ 4MHz, 1000000 / 4 = 3.9k
     TMR0IE_bit:=1;
     
     LM358_diff:=cLM358_diff;
     Delay_10ms;
     {$ifdef Write_OPAMP_DIFF}
     if Write_OPAMP=0 then begin
       Delay_100ms;
       Delay_100ms;
       Delay_100ms;
       adc_cur:=ADC_Read(0);
       EEPROM_Write(0, Lo(adc_cur));
     end;
     {$endif}
     
     {$ifdef USE_EEPROM_LM358DIFF}
     Delay_100ms;
     LM358_diff:=EEPROM_Read(0);
     {$endif}
     Inc(LM358_diff);
     
     {$ifdef USE_4MHZ}
     T1CKPS0_bit:=1;               // timer prescaler 1:2
     {$else}
     T1CKPS1_bit:=1;               // timer prescaler 1:4
     {$endif}
     TMR1CS_bit:=0;
     TMR1L:=TMR1L_LOAD;
     TMR1H:=TMR1H_LOAD;

     adc_vol:=0;
     adc_cur:=0;
     power_curr:=0;
     vol1:=0;
     vol2:=0;
     Reset_Tick100:=100;
     Reset_Tick:=Reset_Tick_Start;

     GIE_bit:=1;                   // enable Interrupt

     TMR1ON_bit:=1;

     VOL_PWM:=PWM_MIN;
     lo_PWM:=PWM_MIN;
     hi_PWM:=PWM_MAX;
     flag_inc:=True;
     p_equal:=False;
     cur_prev:=0;
     vol_prev:=0;
     

     while True do begin
       // LED
       if T1IF_bit=1 then begin
         TMR1H:=TMR1H_LOAD;
         TMR1L:=TMR1L_LOAD;
         T1IF_bit:=0;
         Inc(TICK_1000);
         if TICK_1000>=LED1_tm then begin
           LED1:=not LED1;
           TICK_1000:=0;
         end;
         {$ifdef USE_RESET_TIMER}
         // reset tick
         if Reset_Tick100<>0 then begin
           Dec(Reset_Tick100);
         end else begin
           Reset_Tick100:=100;
           if Reset_Tick<>0 then begin
             Dec(Reset_Tick);
           end else begin
             Reset_Tick:=Reset_Tick_Start;
             VOL_PWM:=PWM_MIN;
             power_curr:=0;
             Inc_pwm:=Inc_Pwm_Max;
             flag_inc:=True;
             vol2:=0;
             lo_PWM:=PWM_MIN;
             hi_PWM:=PWM_MAX;
             Delay_1ms;
             continue;
           end;
         end;
         {$endif}
       end;
       if (VOL_PWM>=(PWM_MAX-1)) then
         LED1_tm:=64
         else if (VOL_PWM<=lo_PWM) then
           LED1_tm:=90
           else
             LED1_tm:=120;
       // save previous adc values
       power_prev:=power_curr;
       cur_prev:=adc_cur;
       vol_prev:=adc_vol;
       // read adc value
       adc_vol:=0;
       adc_cur:=0;
       for i:=0 to adc_max_loop-1 do begin
         adc_cur:=adc_cur+ADC_Read(0);
         adc_vol:=adc_vol+ADC_Read(2);
       end;
       adc_vol:=adc_vol div adc_max_loop;
       adc_cur:=adc_cur div adc_max_loop;
       
       adc_vol:=adc_vol * VOLMUL;
       //
       if adc_cur>LM358_diff then begin
         power_curr:= adc_cur * adc_vol;
         if power_curr=power_prev then begin
           LED1_tm:=240;
           vol2:=0;
           Inc_pwm:=1;
           // get high voltage
           if (not p_equal) or (adc_cur=cur_prev) then begin
             if adc_vol<vol_prev then
               flag_inc:=True
               else if adc_vol>vol_prev then
                 flag_inc:=False
                 else if adc_cur=cur_prev then begin
                   p_equal:=True;
                   continue;
                 end;
             if p_equal and (adc_cur=cur_prev) then
                flag_inc:=not flag_inc;
           end;
           // get low current
           if p_equal or (adc_vol=vol_prev) then begin
             if adc_cur<cur_prev then
               flag_inc:=False
               else if adc_cur>cur_prev then
                 flag_inc:=True
                 else if adc_vol=vol_prev then begin
                   p_equal:=True;
                   continue;
                 end;
             if (not p_equal) and (adc_vol=vol_prev) then
               flag_inc:=not flag_inc;
           end;
           p_equal:=True;
         end else begin
           p_equal:=False;
           {$ifdef USE_OLD_METHOD}
           if Inc_pwm<Inc_Pwm_Max then
             Inc(Inc_pwm);
           {$endif}
           if power_curr<power_prev then begin
             flag_inc:=not flag_inc;
             {$ifdef USE_OLD_METHOD}
             { vol1 = last low PWM }
             vol1:=VOL_PWM;
             { if set last low and high PWM, make average }
             if vol2<>0 then begin
               Inc_pwm:=1;
               wPWM:=vol1;
               wPWM:=(wPWM+vol2+1) div 2+3; { >=3 }
               if (Hi(wPWM)<>0) or (Lo(wPWM)>PWM_MAX) then
                 wPWM:=word(PWM_MAX)
                 else if Lo(wPWM)<PWM_MIN then
                   wPWM:=word(PWM_MIN);

               VOL_PWM:=lo(wPWM);

               vol2:=0;
               //
               wPWM:=(word(PWM_MAX-VOL_PWM)+((PWMHI_DIV+1) div 2)) div PWMHI_DIV;
               wPWM:=wPWM+word(VOL_PWM);
               if (Hi(wPWM)<>0) or (Lo(wPWM)>PWM_MAX) then
                 wPWM:=PWM_MAX;
               hi_PWM:=lo(wPWM);
               //
               wPWM:=(word(VOL_PWM-PWM_MIN)+((PWMLO_DIV+1) div 2)) div PWMLO_DIV;
               wPWM:=word(VOL_PWM)-wPWM;
               if (Hi(wPWM)<>0) or (Lo(wPWM)<PWM_MIN) then
                 wPWM:=PWM_MIN;
               lo_PWM:=lo(wPWM);
               continue;
             end;
             {$endif}
           end else begin
             { last high PWM }
             vol2:=VOL_PWM;
           end;
         end;
       end else begin
         // no load or low current
         power_curr:=0;
         flag_inc:=True;
         adc_cur:=0;
         adc_vol:=0;
         p_equal:=False;
         lo_PWM:=PWM_MIN;
         hi_PWM:=PWM_MAX;
         {$ifdef USE_OLD_METHOD}
         vol2:=0;
         Inc_pwm:=Inc_Pwm_Max;
         {$endif}
       end;
       // PWM
       if flag_inc then begin
         {$ifdef USE_OLD_METHOD}
         if VOL_PWM<(hi_PWM-Inc_pwm) then begin
           VOL_PWM:=VOL_PWM+Inc_pwm;
         {$else}
         if VOL_PWM<PWM_MAX then begin
           Inc(VOL_PWM);
         {$endif}
         end else begin
             VOL_PWM:=hi_PWM;
             flag_inc:=False;
         end;
       end else begin
         {$ifdef USE_OLD_METHOD}
         if (VOL_PWM-lo_PWM)>(Inc_Pwm_Max+1-Inc_pwm) then begin
           VOL_PWM:=VOL_PWM-(Inc_Pwm_Max+1-Inc_pwm);
         {$else}
         if VOL_PWM>PWM_MIN then begin
           Dec(VOL_PWM);
         {$endif}
         end else begin
             VOL_PWM:=lo_PWM;
             flag_inc:=True;
         end;
       end;
     end;
end.